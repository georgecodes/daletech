apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group='com.elevenware'
version='1.0-SNAPSHOT'
archivesBaseName='daletech'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
 testCompile "junit:junit:4.11"
 compile "org.codehaus.groovy:groovy-all:2.4.0-beta-1"
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'daletech'
                packaging 'jar'
                // optionally artifactId can be defined here
                url 'http://www.georgemcintosh.com/daletech'

                scm {
                    connection 'scm:git:git://github.com/georgecodes/daletech.git'
                    developerConnection 'scm:git:git://github.com/georgecodes/daletech.git'
                    url 'scm:git:git://github.com/georgecodes/daletech.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'georgemc'
                        name 'George McIntosh'
                        email 'george@georgemcintosh.com'
                    }
                }
            }
        }
    }
}
